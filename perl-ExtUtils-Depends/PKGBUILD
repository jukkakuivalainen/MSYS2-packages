# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=ExtUtils-Depends
pkgname=perl-${_realname}
pkgver=0.405
pkgrel=1
pkgdesc="The Perl depends module"
arch=('any')
url="http://search.cpan.org/dist/${_realname}/"
groups=('perl-modules')
license=('PerlArtistic')
depends=('perl')
options=('!emptydirs')
source=("https://www.cpan.org/authors/id/X/XA/XAOC/${_realname}-${pkgver}.tar.gz"
        'ExtUtils-Depends-0.304.patch')
sha256sums=('8ad6401ad7559b03ceda1fe4b191c95f417bdec7c542a984761a4656715a8a2c'
            '4a476b3a0e05484e4807cdca13e654fa69b6128f257807d893bb3a39c924a9e6')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/ExtUtils-Depends-0.304.patch
}

build() {
  cd ${_realname}-${pkgver}
  perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_realname}-${pkgver}
  make test
}

package() {
  cd ${_realname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
