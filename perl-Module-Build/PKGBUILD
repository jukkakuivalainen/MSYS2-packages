# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=Module-Build
pkgname=perl-${_realname}
pkgver=0.4224
pkgrel=1
pkgdesc="Build, test, and install Perl modules"
arch=('any')
url="http://search.cpan.org/dist/Module-Build"
license=('PerlArtistic' 'GPL')
depends=('perl>=5.8.0' 'perl-CPAN-Meta>=2.142060' 'perl-inc-latest')
options=('!emptydirs')
source=(https://www.cpan.org/authors/id/L/LE/LEONT/${_realname}-${pkgver}.tar.gz
        module-build-0.4224-msysize.patch)
sha512sums=('c08e84c542a882aa16a682a81cbb7a4860774a3f6a0ec2e720ba9e9bcea76dab331999fee86bf8b92edc08560d7cef4a7c2dfa4e626ab9b22da965e715af531f'
            '543439db152a56fb4881e4881637e2ef8bf30443436857bae32a4f69dfb033490e271c3fb965763b64e5691d362076cd271ad4ef161fc58219bfae6a6dd59902')

prepare() {
  cd ${_realname}-${pkgver}
  patch -p1 -i ${srcdir}/module-build-0.4224-msysize.patch
}

build() {
  cd ${_realname}-${pkgver}
  perl Build.PL installdirs=vendor
  perl Build
}

check() {
  cd ${_realname}-${pkgver}
  perl Build test
}

package() {
  cd ${_realname}-${pkgver}
  perl Build install destdir="${pkgdir}"
}
